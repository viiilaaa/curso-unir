def waitForPort(int port, int retries = 30, int delay = 1) {
    for (int i = 0; i < retries; i++) {
        def result = sh(
            script: "nc -z localhost ${port}",
            returnStatus: true
        )
        if (result == 0) {
            echo "Port ${port} is UP!"
            return
        }
        echo "Waiting for port ${port}..."
        sleep delay
    }
    error "ERROR: Timeout waiting for port ${port}"
}

pipeline {
    agent {label 'agent1'}

    options { skipDefaultCheckout() }

    stages {
        stage('Get code') {
            steps {
                echo 'Obtenemos el cÃ³digo fuente'
                git branch: 'feature_fix_coverage', url: 'https://github.com/viiilaaa/curso-unir'
                echo WORKSPACE
                stash name:'codigo', includes:'**'
            }
            post { always { cleanWs() } }
        }
        stage('Coverage') {
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    unstash name:'codigo'
                    sh '''
                        export PYTHONPATH=${WORKSPACE}/Caso_practico1_1
                        python -m coverage run --branch --source=app --omit=Caso_practico1_1/app/__init__.py,Caso_practico1_1/app/api.py -m pytest --junitxml=result-unit.xml Caso_practico1_1/test/unit
                        python -m coverage xml
                    '''
                }
                stash name:'unit-result', includes:'result-unit.xml'
                recordCoverage qualityGates: [[criticality: 'NOTE', integerThreshold: 95, metric: 'LINE', threshold: 95.0], [criticality: 'ERROR', integerThreshold: 85, metric: 'LINE', threshold: 85.0], [criticality: 'NOTE', integerThreshold: 90, metric: 'BRANCH', threshold: 90.0], [criticality: 'ERROR', integerThreshold: 80, metric: 'BRANCH', threshold: 80.0]], tools: [[parser: 'COBERTURA', pattern: 'coverage.xml']]
            }
            post { always { cleanWs() } }
        }
        stage('Unit'){
            steps {
                unstash name:'unit-result'
                junit 'result-unit.xml'
            }
            post { always { cleanWs() } }
        }
        stage('Rest') {
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    unstash name:'codigo'
                    sh '''
                        export FLASK_APP=Caso_practico1_1/app/api.py                          
                        flask run &
                    '''

                    waitForPort(5000)
                    
                    sh '''
                        java -jar ~/wiremock/wiremock-standalone-3.13.2.jar --port 9090 --root-dir Caso_practico1_1/test/wiremock &
                    '''

                    waitForPort(9090)

                    sh '''
                        export PYTHONPATH=${WORKSPACE}/Caso_practico1_1
                        pytest --junitxml=result-rest.xml Caso_practico1_1/test/rest
                    '''
                }
                junit 'result-rest.xml'
            }
            post { 
                always { 
                    sh 'pkill -9 -f flask || true'
                    sh 'pkill -9 -f wiremock || true'
                    cleanWs() 
                } 
            }
        }  
        stage('Static') {
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    unstash name:'codigo'
                    sh '''
                        python -m flake8 --exit-zero --format=pylint Caso_practico1_1/app >flake8.out
                        '''
                    recordIssues qualityGates: [[threshold: 8, type: 'TOTAL', unstable: true], [threshold: 10, type: 'TOTAL', unstable: false]], tools: [flake8(name: 'Flake8',pattern: 'flake8.out')]
                }
            }
            post { always { cleanWs() } }    
        }  
        stage('Security'){
            agent any
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                unstash name:'codigo'
                bat '''
                        bandit --exit-zero -r Caso_practico1_1 -f custom -o bandit.out --msg-template "{abspath}:{line}: [{test_id}] {severity}: {msg}"
                    '''
                    recordIssues qualityGates: [[threshold: 2, type: 'TOTAL', unstable: true], [threshold: 4, type: 'TOTAL', unstable: false]], tools: [pyLint(name: 'Bandit',pattern: 'bandit.out')]
                }
            }
            post { always { cleanWs() } }
        }
        stage('Performance') {
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    unstash name:'codigo'
                    sh '''
                        export FLASK_APP=Caso_practico1_1/app/api.py                          
                        flask run &
                    '''
                    waitForPort(5000)

                    sh '''
                        ~/apache-jmeter-5.6.3/bin/jmeter -n -t Caso_practico1_1/test/jmeter/flask_cp12.jmx -f -l flask.jtl
                        '''
                }
                perfReport sourceDataFiles: 'flask.jtl'
            }
            post { 
                always { 
                    sh 'pkill -9 -f flask || true'
                    cleanWs() 
                } 
            }
        }  
    }
}